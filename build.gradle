buildscript {
    repositories {
    	maven { url  = 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
	id 'fabric-loom' version '1.4-SNAPSHOT'
	id 'maven-publish'
	id 'net.darkhax.curseforgegradle' version '1.+'
    id "com.modrinth.minotaur" version "2.+"
}

import com.modrinth.minotaur.dependencies.ModDependency

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
	filesMatching("fabric.mod.json") {
		expand(
				"mod_version": version,
				"minecraft_version": project.minecraft_version,
				"loader_version": project.loader_version,
		)
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {}
}

if (file('key.properties').exists()) {
	task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
		def curseProp = new Properties()
		File secretPropsFile = file("key.properties")
		curseProp.load(secretPropsFile.newInputStream())

		apiToken = curseProp.getProperty("curseKey")

		def mainFile = upload("858703", remapJar)
		mainFile.releaseType = "release"
		mainFile.changelogType = "text"
		mainFile.changelog = file("changelog.txt")
		mainFile.addGameVersion("1.20.1")
		mainFile.addGameVersion("1.20.2")
		mainFile.addModLoader("Fabric")
		mainFile.addJavaVersion("Java 17")

		mainFile.addRequirement "fabric-api"
	}
}

if (file('key.properties').exists()) {
	modrinth {
		def modrinthProp = new Properties()
		File secretPropsFile = file("key.properties")
		modrinthProp.load(secretPropsFile.newInputStream())

		token = modrinthProp.getProperty('modrinthKey')
		projectId = 'sUlkLN1E'
		versionNumber = project.mod_version
		uploadFile = remapJar
		changelog = rootProject.file("changelog.txt").text
		gameVersions = ["1.20.1", "1.20.2"]
		versionType = "release"
		loaders = ['fabric', 'quilt']
		dependencies {
			required.project "fabric-api"
		}
	}
}